apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
    }
}

group = 'com.sandrios.android'
version = '1.0.9'

ext {
    PUBLISH_GROUP_ID = 'com.sandrios.android'
    PUBLISH_ARTIFACT_ID = 'sandriosCamera'
    PUBLISH_VERSION = '1.0.9'
    PUBLISH_CODE = 10
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode PUBLISH_CODE
        versionName PUBLISH_VERSION
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
    archives generateSourcesJar
    archives generateJavadocsJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name PUBLISH_GROUP_ID
            description 'Simple integration of universal camera in android for easy image and video capture.'
            url 'https://github.com/sandrios/sandriosCamera'
            inceptionYear '2016'

            packaging 'aar'
            version PUBLISH_VERSION

            scm {
                connection 'https://github.com/sandrios/sandriosCamera.git'
                url 'https://github.com/sandrios/sandriosCamera'

            }
            developers {
                developer {
                    name 'arpitgandhi9'
                }
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')
    configurations = ['archives']
    pkg {
        repo = 'android'
        name = 'sandriosCamera'
        userOrg = 'sandriosstudios'
        desc = 'Android solution to simplify work with different camera apis.'
        licenses = ['MIT']
        labels = ['android', 'camera', 'photo', 'video']
        websiteUrl = 'https://github.com/sandrios/sandriosCamera'
        issueTrackerUrl = 'https://github.com/sandrios/sandriosCamera/issues'
        vcsUrl = 'https://github.com/sandrios/sandriosCamera.git'
        version {
            name = PUBLISH_VERSION
            vcsTag = PUBLISH_VERSION
            desc = 'Minor fixes.'
            released = new Date()
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile 'com.android.support:support-v4:25.1.1'
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:recyclerview-v7:25.1.1'
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'com.yalantis:ucrop:2.2.0'
    compile 'gun0912.ted:tedpermission:1.0.2'
}

